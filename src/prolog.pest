WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

operators = {
    "\\+" | "+" | "-" | "**" | "*" | "/" | "^" | "<" | ">" | "=" | "=:="
}
atom = @{ (ASCII_ALPHA_LOWER ~ ( "_" | ASCII_ALPHANUMERIC)*) | operators }
variable = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC*}

structure = { atom ~ "(" ~ terms ~ ")" }


list_tail = { "|" ~ variable }
list = { "[" ~ terms?  ~  list_tail? ~ "]"  }

infix_expression = { basic_term ~ (atom ~ basic_term)+ }

basic_term = {
    structure | list | number | atom | variable | string
}

term = _{
    infix_expression | basic_term
}

terms = _{
    term ~ ( "," ~ term)*
}

fact = { term ~ "." }
rule = { term ~ ":-" ~ terms ~ "." }
query = { terms ~ "." }

file = { SOI ~ (fact | rule)* ~ EOI }