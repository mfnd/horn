WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

operator_symbol = @{
    ("+" | "-" | "*" | "/" | "^" | "<" | ">" | "=")+ | "\\+" | "=:="
}

atom = @{ (ASCII_ALPHA_LOWER ~ ( "_" | ASCII_ALPHANUMERIC)*) | operator_symbol }
variable = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC*}


list_tail = { "|" ~ variable }
list = { "[" ~ terms?  ~  list_tail? ~ "]"  }

paren_expr = { "(" ~ expression  ~ ")" }
expression = { ( paren_expr | basic_term )+ }

struct_head = ${ atom ~ "(" }
structure = { struct_head ~ expression ~ ( "," ~ expression)* ~ ")" }

basic_term = {
    structure | list | number | atom | variable | string
}

term = _{
    expression
}

terms = _{
    term ~ ( "," ~ term)*
}

fact = { term ~ "." }
rule = { term ~ ":-" ~ terms ~ "." }
query = { terms ~ "." }

file = { SOI ~ (fact | rule)* ~ EOI }

term_only = _{ SOI ~ term ~ EOI}