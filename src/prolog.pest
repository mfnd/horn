WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

operators = {
    "+" | "-" | "*" | "/"
}
atom = @{ (ASCII_ALPHA_LOWER ~ ( "_" | ASCII_ALPHANUMERIC)*) | operators }
variable = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC*}

structure = { atom ~ "(" ~ terms ~ ")" }


list_tail = { "|" ~ variable }
list = { "[" ~ terms?  ~  list_tail? ~ "]"  }

term = {
    structure | list | number | atom | variable | string
}

terms = _{
    term ~ ( "," ~ term)*
}

fact = { term ~ "." }
rule = { term ~ ":-" ~ terms ~ "." }
query = { terms ~ "." }

file = { SOI ~ (fact | rule)* ~ EOI }